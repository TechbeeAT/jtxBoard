/*
 * Copyright (c) Techbee e.U.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU Public License v3.0
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl.html
 */

apply plugin: 'com.android.application'
apply plugin: 'com.mikepenz.aboutlibraries.plugin'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: "kotlinx-serialization"


android {
    namespace 'at.techbee.jtx'
    compileSdkVersion 33
    defaultConfig {
        applicationId "at.techbee.jtx"
        buildConfigField "long", "buildTime", System.currentTimeMillis() + "L"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 201030007
        versionName "2.01.03-rc07"      // keep -release as a suffix also for release, build flavor adds the suffix e.g. .gplay (e.g. 1.00.00-rc0.gplay)
        buildConfigField "String", "versionCodename", "\"Stop Putin, stop Raisi - peace for Ukraine, liberty for Iranians!\""
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // see https://developer.android.com/studio/write/java8-support
        kotlinOptions {
            jvmTarget = "1.8"
        }
        testOptions {
            unitTests.includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
        // defining the room.schemaLocation explicitly allows AutoMigrations
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }

        def locales = getLocales()
        buildConfigField "String[]", "TRANSLATION_ARRAY", "new String[]{\""+locales.join("\",\"")+"\"}"
        resConfigs locales
    }

    signingConfigs {
        jtx {
            storeFile file(System.getenv("ANDROID_KEYSTORE") ?: "/dev/null")
            storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
            keyAlias System.getenv("ANDROID_KEY_ALIAS")
            keyPassword System.getenv("ANDROID_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.jtx
        }
        // uncomment to test minifyEnabled without signing the package
        debug {
            //minifyEnabled = true
            //shrinkResources = true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Enables data binding.
    buildFeatures {
        dataBinding true
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.3.2"
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/*.md']
            pickFirsts += ['META-INF/AL2.0', 'META-INF/LGPL2.1']
        }
    }

    flavorDimensions 'version'
    productFlavors {
        gplay {
            dimension 'version'
            versionNameSuffix '.gplay'
        }
        ose {
            dimension 'version'
            versionNameSuffix '.ose'
        }
        generic {
            dimension 'version'
            versionNameSuffix '.generic'
        }
    }

    // the flavor alphatest should not be used for the build type release, we filter this here
    variantFilter { variant ->
        if (variant.flavors*.name.contains("alphatest") && variant.buildType.name == "release") {
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs, this is especially necessary to use java.time for versions < O
        // https://developer.android.com/studio/write/java8-support#library-desugaring
        // also good for ical4android
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lint {
        disable 'MissingTranslation'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    sourceSets {
        main {
            res.srcDirs += ['build/generated/res/locale']
        }
    }
}


import groovy.xml.MarkupBuilder

import static groovy.io.FileType.DIRECTORIES

/**
 * Obtains a list of all the available locales
 * @since 20220928
 * @return A list with the language codes of the locales available.
 */
def getLocales() {
    // Initialize the list English, since it's available by default
    def list = ["en"]
    // Get all directories inside resources
    def dir = new File(projectDir, "src/main/res")
    dir.traverse(type: DIRECTORIES, maxDepth: 0) { file ->
        // Get only values directories
        def fileName = file.name
        if (!fileName.startsWith("values-")) return

        // Take only the values directories that contain strings
        def stringsFile = new File(file, "strings.xml")
        if (!stringsFile.exists()) return

        // Add to the list the locale of the strings file
        list.add(fileName.substring(fileName.indexOf('-') + 1))
    }
    // Log the available locales
    println "Supported locales: " + list.join(", ")
    // Return the built list
    return list
}

/**
 * Writes the available locales obtained from getLocales() in locale-config.xml
 * @since 20221016
 * @return A list with the language codes of the locales available.
 */
task updateLocalesConfig() {
    println 'Building locale config...'
    ext.outputDir = new File(projectDir, 'build/generated/res/locale/xml')

    doFirst {
        mkdir outputDir

        new File(outputDir, "locales_config.xml").withWriter { writer ->
            def destXml = new MarkupBuilder(new IndentPrinter(writer, "    ", true, true))
            destXml.setDoubleQuotes(true)
            def destXmlMkp = destXml.getMkp()
            destXmlMkp.xmlDeclaration(version: "1.0", encoding: "utf-8")
            destXmlMkp.comment("Generated at ${new Date()}")
            destXmlMkp.yield "\r\n"

            def locales = getLocales()
            destXml."locale-config"(['xmlns:android':"http://schemas.android.com/apk/res/android"]) {
                locales.forEach { locale ->
                    destXml."locale"("android:name": locale)
                }
            }
        }
    }
}

gradle.projectsEvaluated {
    preBuild.dependsOn('updateLocalesConfig')
}


dependencies {

    implementation project(':ical4android')

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android KTX
    implementation "androidx.core:core-ktx:$version_core"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1"

    // Room and Lifecycle dependencies
    implementation "androidx.room:room-runtime:$version_room"
    implementation "androidx.room:room-ktx:$version_room"   // Kotlin Extensions and Coroutines support for Room
    kapt "androidx.room:room-compiler:$version_room"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutine"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_coroutine"

    implementation "androidx.preference:preference-ktx:1.2.0"

    // Volley for simple network requests
    implementation "com.android.volley:volley:1.2.1"

    // Google billing
    gplayImplementation "com.android.billingclient:billing-ktx:$version_billing"
    // Google request for review
    gplayImplementation "com.google.android.play:review:$version_review"
    gplayImplementation "com.google.android.play:review-ktx:$version_review"

    // Google Maps
    gplayImplementation "com.google.maps.android:maps-compose:2.2.0"
    gplayImplementation "com.google.android.gms:play-services-maps:18.1.0"
    gplayImplementation "com.google.android.gms:play-services-location:21.0.1"

    // Workmanager support
    implementation "androidx.work:work-runtime-ktx:$version_work"
    androidTestImplementation "androidx.work:work-testing:$version_work"

    //support for java.time for older Android versions (<O)
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.5")

    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.4"
    //androidTestImplementation "androidx.test.espresso:espresso-core:3.5.0-alpha07"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$version_coroutine"
    androidTestImplementation "androidx.test:runner:1.5.1"
    androidTestImplementation "androidx.test:rules:1.5.0"

    // Testing-only dependencies
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$version_kotlin"
    androidTestImplementation 'androidx.test:core-ktx:1.5.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.room:room-testing:$version_room"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'androidx.test:core-ktx:1.5.0'
    // Required -- JUnit 4 framework
    testImplementation "androidx.test.ext:junit-ktx:1.1.4"
    testImplementation "org.mockito:mockito-core:3.9.0"

    //About Libraries
    implementation "com.mikepenz:aboutlibraries-core:${version_about_libraries}"
    implementation "com.mikepenz:aboutlibraries-compose:${version_about_libraries}"

    // Compose Color Picker
    implementation "com.godaddy.android.colorpicker:compose-color-picker-android:0.5.1"

    // appcompat for language selection
    implementation "androidx.appcompat:appcompat:$version_appcompat"

    // jetpack compose
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "androidx.compose.ui:ui:$version_compose"
    implementation "androidx.compose.material3:material3:1.0.1"
    //implementation "androidx.compose.material3:material3-window-size-class:1.0.0-beta02"

    implementation "androidx.compose.material:material:1.3.1"    // material 2 support as bottom sheet is missing in m3
    implementation "androidx.compose.material:material-icons-extended:1.3.1"
    implementation "androidx.compose.ui:ui-tooling-preview:$version_compose"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"
    //implementation "androidx.activity:activity-compose:1.6.0"
    implementation "androidx.compose.runtime:runtime-livedata:$version_compose"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1"
    implementation "androidx.navigation:navigation-compose:2.6.0-alpha04"
    implementation 'com.arnyminerz.markdowntext:markdowntext:1.1.1'

    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$version_compose"
    debugImplementation "androidx.compose.ui:ui-tooling:$version_compose"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$version_compose"

    // permissions from accompanist
    implementation "com.google.accompanist:accompanist-permissions:$version_accompanist"
    // Paging Compose
    implementation "com.google.accompanist:accompanist-pager:$version_accompanist"
    implementation "com.google.accompanist:accompanist-pager-indicators:$version_accompanist"
    implementation "com.google.accompanist:accompanist-flowlayout:$version_accompanist"

    implementation "androidx.glance:glance-appwidget:1.0.0-alpha05"
}

